#################
### DOTFILES ####
#################

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files in this repo
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# configure homebrew completions: https://docs.brew.sh/Shell-Completion#configuring-completions-in-zsh
if type brew &>/dev/null; then
  FPATH=$(brew --prefix)/share/zsh/site-functions:$FPATH
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

#################
### OH MY ZSH ###
#################

# Path to oh-my-zsh configuration.
ZSH=$HOME/.oh-my-zsh
ZSH_THEME="steeef"
DISABLE_AUTO_UPDATE="false"
COMPLETION_WAITING_DOTS="true"
DISABLE_AUTO_TITLE=true

# use the following oh-my-zsh plugins
plugins=(asdf brew git gitfast npm vi-mode fzf)

# load oh-my-zsh
source $ZSH/oh-my-zsh.sh

####################
### CUSTOM ALIAS ###
####################

custom_alias=($DOTFILES/alias/*.zsh)
for file in ${(M)custom_alias}
do
  source $file
done
